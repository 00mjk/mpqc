define([sc_version],[2.0.beta.5])

dnl Process this file with autoconf to produce a configure script.
define([AC_CACHE_LOAD], )dnl for debugging configure.in
define([AC_CACHE_SAVE], )dnl for debugging configure.in
AC_INIT(src/lib/util/ref/ref.h)
AC_PREREQ(2.13)
AC_CONFIG_HEADER(src/lib/scconfig.h)
AC_CONFIG_AUX_DIR(bin)

AC_CANONICAL_SYSTEM

AC_DEFINE_UNQUOTED(HOST_ARCH, "$host")
AC_DEFINE_UNQUOTED(TARGET_ARCH, "$target")
define([default_prefix],builtin(format,"/usr/local/mpqc/%s",sc_version))
AC_PREFIX_DEFAULT(default_prefix)

SC_VERSION=sc_version
AC_DEFINE_UNQUOTED(SC_VERSION, "$SC_VERSION")
AC_SUBST(SC_VERSION)

dnl --------- Features ---------

AC_ARG_ENABLE(debug,
[  --enable-debug          Compile with debugging options],
[
case $enableval in
  yes)
    DEBUG=yes
  ;;
  no)
    DEBUG=no
  ;;
  opt)
    DEBUG=opt
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-debug ($enableval)])
  ;;
esac
],[
    DEBUG=no
]
)

AC_ARG_ENABLE(strict-arch,
[  --enable-strict-arch    Code might only work on target cpu type],
[
case $enableval in
  yes)
    STRICT_ARCH=yes
  ;;
  no)
    STRICT_ARCH=no
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-strict-arch ($enableval)])
  ;;
esac
],[
    STRICT_ARCH=no
]
)

AC_ARG_ENABLE(stl,
[  --enable-stl            Use the Standard Template Library.],
[
case $enableval in
  yes)
    AC_DEFINE(HAVE_STL)
  ;;
  no)
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-stl ($enableval)])
  ;;
esac
])

AC_ARG_ENABLE(parallel,
[  --disable-parallel      Compile for serial execution.],
[
case $enableval in
  yes)
    PARALLEL=yes
  ;;
  no)
    PARALLEL=no
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-parallel ($enableval)])
  ;;
esac
],[
    PARALLEL=yes
]
)

AC_ARG_ENABLE(long-long,
[  --disable-long-long     Disable use of long long.],
[
case $enableval in
  yes)
    LONGLONG=yes
  ;;
  no)
    LONGLONG=no
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-long-long ($enableval)])
  ;;
esac
],[
    LONGLONG=yes
]
)

AC_ARG_ENABLE(threads,
[  --enable-threads        Compile allowing use of threads, if possible.],
[
case $enableval in
  yes)
    THREADS=yes
  ;;
  no)
    THREADS=no
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-threads ($enableval)])
  ;;
esac
],[
    THREADS=yes
]
)

AC_ARG_ENABLE(ref-debug,
[  --enable-ref-debug      Check for reference count overwrites/overflows/etc],
[
case $enableval in
  yes)
  ;;
  no)
    AC_DEFINE(REF_OPTIMIZE)
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-ref-debug ($enableval)])
  ;;
esac
],[
  if test $DEBUG = no -o $DEBUG = opt; then
    AC_DEFINE(REF_OPTIMIZE)
  fi
]
)

AC_ARG_ENABLE(ref-macros,
[  --disable-ref-macros    Do not use macros to declare ref classes.],
[
case $enableval in
  yes)
    AC_DEFINE(USE_REF_MACROS)
  ;;
  no)
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-ref-macros ($enableval)])
  ;;
esac
],[
  AC_DEFINE(USE_REF_MACROS)
]
)

AC_ARG_ENABLE(cross-compile,
[  --enable-cross-compile  Rather than checking, assume cross compilation.],
[
case $enableval in
  yes)
    cross_compiling=yes
  ;;
  no)
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-cross-compile ($enableval)])
  ;;
esac
])

enableshared=no
AC_ARG_ENABLE(shared,
[  --enable-shared         Build shared libraries.],
[
case $enableval in
  yes)
    enableshared=yes
  ;;
  no)
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --(dis|en)able-shared ($enableval)])
  ;;
esac
])

enablestatic=yes
AC_ARG_ENABLE(static,
[  --disable-static        Do not build static libraries.],
[
case $enableval in
  yes)
  ;;
  no)
    enablestatic=no
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --(dis|en)able-static ($enableval)])
  ;;
esac
])

enablelibtool=yes
AC_ARG_ENABLE(libtool,
[  --disable-libtool       Do not use libtool.],
[
case $enableval in
  yes)
  ;;
  no)
    enablelibtool=no
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --(dis|en)able-libtool ($enableval)])
  ;;
esac
])

AC_ARG_WITH(cc,
[  --with-cc               Gives the name of the C compiler to use.],
CC=$withval
)

AC_ARG_WITH(cxx,
[  --with-cxx              Gives the name of the C++ compiler to use.],
CXX=$withval
)

AC_ARG_WITH(f77,
[  --with-f77              Gives the name of the FORTRAN 77 compiler to use.],
F77=$withval
)

AC_ARG_WITH(ranlib,
[  --with-ranlib           Gives the name of the ranlib program.],
RANLIB=$withval
)

AC_ARG_WITH(ar,
[  --with-ar               Names the archive creator.],
AR=$withval
)

AC_ARG_WITH(ld,
[  --with-ld               Names the object linker.],
LD=$withval
)

AC_ARG_WITH(include,
[  --with-include          Specifies include directories (-Idir1 -Idir2).],
EXTRAINCLUDE=$withval
CPPFLAGS=$withval
echo Using extra include directories: $withval
)

AC_ARG_WITH(libs,
[  --with-libs             Specifies libraries (-llib1 -llib2).],
LIBS=$withval
echo Using extra libraries: $withval
)

LDFLAGS=
LIBDIRS=
AC_ARG_WITH(libdirs,
[  --with-libdirs          Specifies library directories (-Ldir1 -Ldir2).],
LIBDIRS=$withval
LDFLAGS=$withval
echo Using extra library directories: $withval
)

dnl --------- Want absolute path for srcdir, not relative. ---------

if test X$ac_srcdir_defaulted = Xyes; then
  case $srcdir in
    ../*)
      srcdir=`(cd $srcdir; pwd)`
      #srcdir=`(cd $srcdir; echo $PWD)`
      ;;
  esac
fi

dnl --------- Checks for programs. ---------
AC_PROG_LN_S
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_CC
dnl ac_prog_cxx's order isn't what i need
AC_CHECK_PROGS(CXX, g++ c++ gcc CC cxx xlC, gcc)
dnl sees if CXX is a GNU compiler
AC_PROG_CXX
changequote(<<, >>)dnl
if [ ! "$F77" = no ]; then
changequote([, ])dnl
  AC_PROG_F77
  AC_F77_LIBRARY_LDFLAGS
fi
AC_PROG_CPP
AC_PROG_CXXCPP
AC_CHECK_PROG(AR,ar,ar)
AC_CHECK_PROG(PERL,perl,perl)
AC_CHECK_PROG(BISON,bison,bison)
AC_CHECK_PROG(FLEX,flex,flex)
AC_CHECK_PROG(WISH,wish,/usr/bin/wish)

dnl The lack of certain tools is a show stopper

changequote(<<, >>)dnl

if [ X$BISON = X ]; then
  echo "Could not find the program bison.  It can be obtained at"
  echo "ftp://prep.ai.mit.edu/pub/gnu"
  exit 1
fi

if [ X$FLEX = X ]; then
  echo "Could not find the program flex.  It can be obtained at"
  echo "ftp://prep.ai.mit.edu/pub/gnu"
  exit 1
fi

if [ X$PERL = X ]; then
  echo "Could not find the program perl.  It can be obtained at"
  echo "ftp://prep.ai.mit.edu/pub/gnu"
  exit 1
fi

changequote([, ])dnl

dnl --------- This is at the top since its likely to cause problems. ---------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_TRY_COMPILE([#include <FlexLexer.h>],[],[
],AC_MSG_ERROR([couldn't find FlexLexer.h--try --with-include]))
AC_LANG_RESTORE

dnl -------- Checks for cross compiling. ----------

# obsolete starting at autoconf 2.12
#if test X$cross_compiling != Xyes; then
#AC_C_CROSS
#fi

#if test X$cross_compiling = Xyes -a X$target = X$host; then
#  AC_MSG_ERROR([Cross compiling, but target is host (use --host).])
#fi

dnl -------- Checks for compiler/linker options. ----------

# options needed only for optimization
COPTIONS_OPT=-O
# options needed only for debugging
COPTIONS_DBG=-g
# options that are always needed
COPTIONS_MISC=

# options needed only for optimization
CXXOPTIONS_OPT=-O
# options needed only for debugging
CXXOPTIONS_DBG=-g
# options that are always needed
CXXOPTIONS_MISC=

OBJSUF=o
LIBSUF=a

dnl -- check how dependency information is built --

# The GNU compilers work with:
CCDEPENDSUF=none
CXXDEPENDSUF=none
CCDEPENDFLAGS=-M
CXXDEPENDFLAGS=-M

/bin/rm -f depcheck.u depcheck.c depcheck.o

# Check for an IBM visual age C compiler
echo "#include <stdio.h>" > depcheck.c
$CC $CPPFLAGS $CFLAGS -M depcheck.c > /dev/null 2>&1
if test -f depcheck.u; then
  CCDEPENDSUF=u
fi
/bin/rm -f depcheck.u depcheck.c depcheck.o

# Check for an IBM visual age C++ compiler
echo "#include <iostream>" > depcheck.cc
$CXX $CPPFLAGS $CXXFLAGS -M -E depcheck.cc > /dev/null 2>&1
if test -f depcheck.u; then
  CXXDEPENDSUF=u
  CXXDEPENDFLAGS="-M -E"
fi
/bin/rm -f depcheck.u depcheck.c depcheck.o

dnl -- special optimization options --

AC_MSG_CHECKING([for special optimization options])
case $target in
  rs6000-ibm-aix3.2.* | rs6000-ibm-aix4.* | powerpc-ibm-aix4.*)
    if test X$GCC != Xyes; then
      COPTIONS_OPT="-O -qnolm"
    fi
    if test X$GXX != Xyes; then
      CXXOPTIONS_OPT="-O -qnolm -qrtti"
    fi
    AC_MSG_RESULT("rs6000 or powerpc")
  ;;
  alphaev6-*)
    if test X$GCC = Xyes; then
      COPTIONS_OPT="-O3 -mcpu=ev6"
    else
      COPTIONS_OPT="-O5 -arch ev6"
    fi
    if test X$GXX = Xyes; then
      CXXOPTIONS_OPT="-O3 -mcpu=ev6"
    else
      CXXOPTIONS_OPT="-O5 -arch ev6"
    fi
    AC_MSG_RESULT("alphaev6")
  ;;
  alphaev56-*)
    if test X$GCC = Xyes; then
      COPTIONS_OPT="-O3 -mcpu=ev56"
    else
      COPTIONS_OPT="-O5 -arch ev56"
    fi
    if test X$GXX = Xyes; then
      CXXOPTIONS_OPT="-O3 -mcpu=ev56"
    else
      CXXOPTIONS_OPT="-O5 -arch ev56"
    fi
    AC_MSG_RESULT("alphaev56")
  ;;
  mips*-sgi-irix5*)
    if test X$GCC != Xyes; then
      COPTIONS_OPT="-O -Olimit 2000"
    fi
    AC_MSG_RESULT("mips*-sgi-irix5")
  ;;
changequote(<<, >>)dnl
  mips*-sgi-irix6.[01]*)
changequote([, ])dnl
    if test X$GCC != Xyes; then
      COPTIONS_OPT="-O2 -TENV:use_fp \
        -OPT:const_copy_limit=20000:fold_arith_limit=20000:global_limit=20000"
    fi
    if test X$GXX != Xyes; then
      CXXOPTIONS_OPT="-O2 -TENV:use_fp \
        -OPT:const_copy_limit=20000:fold_arith_limit=20000:global_limit=20000"
    fi
    AC_MSG_RESULT("mips*-sgi-irix6.0 or mips*-sgi-irix6.1")
  ;;
  mips*-sgi-irix*)
    if test X$GCC != Xyes; then
      COPTIONS_OPT="-O2 -OPT:Olimit=0"
    fi
    if test X$GXX != Xyes; then
      CXXOPTIONS_OPT="-O2 -OPT:Olimit=0"
    fi
    AC_MSG_RESULT("mips*-sgi-irix")
  ;;
changequote(<<, >>)dnl
  i[56]86-*)
changequote([, ])dnl
    if test X$GCC = Xyes; then
      COPTIONS_OPT="-O2"
    fi
    if test X$GXX = Xyes; then
      CXXOPTIONS_OPT="-O2"
    fi
    AC_MSG_RESULT("i586 or i686")
  ;;
  i860-intel-*)
    if test X$GCC != Xyes; then
      COPTIONS_OPT="-O3 -Knoieee"
    fi
    AC_MSG_RESULT("i860")
  ;;
  *)
    AC_MSG_RESULT("none")
  ;;
esac

dnl -- special architecture options --

AC_MSG_CHECKING([for special architecture options])
case $target in
  *-solaris2*)
  if test X$GCC != Xyes; then
    CCDEPENDFLAGS="-xM"
  fi
  if test X$GXX != Xyes; then
    CXXDEPENDFLAGS="-xM"
  fi
  ;;
  rs6000-* | powerpc-*)
    if test X$GCC != Xyes; then
      LDFLAGS="$LDFLAGS -bmaxdata:0x70000000"
    fi
    AC_MSG_RESULT(rs6000 or powerpc)
  ;;
  i686-intel-cougar*)
    COPTIONS_MISC="$COPTIONS_MISC -mcougar"
    CXXOPTIONS_MISC="$CXXOPTIONS_MISC -mcougar"
    EXTRADEFINES="-D_REENTRANT $EXTRADEFINES"
    if test X$cross_compiling = Xyes; then
      AR=tflop-ar
    fi
    AC_MSG_RESULT(teraflop cougar)
  ;;
changequote(<<, >>)dnl
  i[456]86-* | pentium | pentiumpro | k6 | athlon)
changequote([, ])dnl
    if test X$STRICT_ARCH = Xyes; then
      cpu_arch_option="-march=$target_cpu"
    else
      cpu_arch_option="-mcpu=$target_cpu"
    fi
    if test X$GCC = Xyes; then
      COPTIONS_MISC="$COPTIONS_MISC $cpu_arch_option"
    fi
    if test X$GXX = Xyes; then
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC $cpu_arch_option"
    fi
    AC_MSG_RESULT(IA-32: $cpu_arch_option)
  ;;
  i860-intel-puma*)
    if test X$GCC = Xyes; then
      COPTIONS_MISC="$COPTIONS_MISC -mpuma"
    else
      COPTIONS_MISC="$COPTIONS_MISC -D__PUMAGON__"
      COPTIONS_MISC="$COPTIONS_MISC -L$PARAGON_XDEV/paragon/lib-coff/puma"
      COPTIONS_MISC="$COPTIONS_MISC -YS,$PARAGON_XDEV/paragon/lib-coff/puma"
      COPTIONS_MISC="$COPTIONS_MISC -lpuma -lm -lkmath"
      COPTIONS_MISC="$COPTIONS_MISC $PARAGON_XDEV/paragon/lib-coff/puma/libstubs.o"
    fi
    if test X$GXX = Xyes; then
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -mpuma"
    fi
    if test X$cross_compiling = Xyes; then
      AR=ar860
    fi
    AC_MSG_RESULT(paragon puma)
  ;;
  i860-intel-sunmos*)
    if test X$GCC = Xyes; then
      COPTIONS_MISC="$COPTIONS_MISC -msunmos"
    else
      COPTIONS_MISC="$COPTIONS_MISC -DSUNMOS -D__PUMAGON__"
      COPTIONS_MISC="$COPTIONS_MISC -L$PARAGON_XDEV/paragon/lib-coff/sunmos"
      COPTIONS_MISC="$COPTIONS_MISC -Wl,-d0x4000000,-k"
      COPTIONS_MISC="$COPTIONS_MISC -YS,$PARAGON_XDEV/paragon/lib-coff/sunmos"
      COPTIONS_MISC="$COPTIONS_MISC -lm -lsunmos -lm"
    fi
    if test X$GXX = Xyes; then
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -msunmos"
    fi
    if test X$cross_compiling = Xyes; then
      AR=ar860
    fi
    AC_MSG_RESULT(paragon sunmos)
  ;;
  i860-intel-osf*)
    if test X$GCC = Xyes; then
      COPTIONS_MISC="$COPTIONS_MISC -mnx"
    fi
    if test X$GXX = Xyes; then
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -mnx"
    fi
    if test X$cross_compiling = Xyes; then
      AR=ar860
    fi
    AC_MSG_RESULT(paragon osf)
  ;;
  *)
    AC_MSG_RESULT("none")
  ;;
esac

dnl ----------- check for C++ typename ---------------
dnl This is done before template flags are set to avoid
dnl undefined symbol problems.

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING("checking for C++ typename keyword")
AC_TRY_LINK([
  class X { public: typedef int t; X(){} };
  template <class T> void f(T i) {typename T::t x;}
],[
  X g;
  f(g);
],[
AC_DEFINE(HAVE_TYPENAME)
AC_MSG_RESULT("yes")
],
AC_MSG_RESULT("no")
);
AC_LANG_RESTORE

dnl ---------- Checks for C++ header files. -----------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_CHECK_HEADERS(iostream sstream)
AC_LANG_RESTORE

AC_MSG_CHECKING(iostream name)
if test x$ac_cv_header_iostream = xyes; then
  iostream=iostream
else
  iostream=iostream.h
fi
AC_MSG_RESULT($iostream)

dnl ----------- check for namespace std ---------------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING("checking for namespace std")
AC_TRY_LINK([
#include <$iostream>
using namespace std;
],[
  ostream &o = cout;
  o << endl;
],[
AC_DEFINE(USING_NAMESPACE_STD)
NAMESPACE_STD=std::
AC_MSG_RESULT("yes")
],
NAMESPACE_STD=
AC_MSG_RESULT("no")
);
AC_LANG_RESTORE

dnl ----------- check for GNU libc++-v3 prerelease bug ---------------

if test X$GXX = Xyes; then
AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING("checking for GNU libc++-v3 prerelease bug")
AC_TRY_LINK([
#include <stdio.h>
#include <$iostream>
],[
],[
AC_MSG_RESULT("no")
],
EXTRADEFINES="$EXTRADEFINES -D_ISOC99_SOURCE=1"
AC_MSG_RESULT("yes")
);
AC_LANG_RESTORE
fi

dnl -- other options --

AC_MSG_CHECKING([GNU c++ library/template flags])
if test X$GXX = Xyes; then
  # automatic template instantiation causes problems with libstc++-v3
  #CXXOPTIONS_MISC="$CXXOPTIONS_MISC -fno-implicit-templates"
  #EXPLICIT_TEMPLATE_INSTANTIATION=yes
  #AC_DEFINE(EXPLICIT_TEMPLATE_INSTANTIATION)
  AC_MSG_RESULT("GNU")
else
changequote(<<, >>)dnl
  case $target in
    *-sgi-irix6.0*)
      AC_MSG_RESULT("*-sgi-irix6.0*")
    ;;
    *-sgi-irix*)
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -ptused"
      AC_MSG_RESULT("*-sgi-irix*")
      ;;
    *)
      AC_MSG_RESULT("generic non GNU")
      ;;
    esac
changequote([, ])dnl
fi

if test $DEBUG = yes; then
  CFLAGS="$COPTIONS_DBG $COPTIONS_MISC"
  CXXFLAGS="$CXXOPTIONS_DBG $CXXOPTIONS_MISC"
  LDFLAGS="$LDFLAGS -g"
elif test $DEBUG = opt; then
  CFLAGS="$COPTIONS_DBG $COPTIONS_OPT $COPTIONS_MISC"
  CXXFLAGS="$CXXOPTIONS_DBG $COPTIONS_OPT $CXXOPTIONS_MISC"
  LDFLAGS="$LDFLAGS -g"
else
  CFLAGS="$COPTIONS_OPT $COPTIONS_MISC"
  CXXFLAGS="$CXXOPTIONS_OPT $CXXOPTIONS_MISC"
fi

AC_SUBST(EXTRAINCLUDE)

AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)

AC_SUBST(LDFLAGS)
AC_SUBST(LIBDIRS)

AC_SUBST(OBJSUF)
AC_SUBST(LIBSUF)
AC_SUBST(CCDEPENDSUF)
AC_SUBST(CXXDEPENDSUF)
AC_SUBST(CCDEPENDFLAGS)
AC_SUBST(CXXDEPENDFLAGS)

dnl -------- Checks for architecture specific features. --------
dnl (Doesn't work for cross compilation.)
dnl AC_CHECK_SIZEOF(void*)

dnl This version works on more machines without the need to run
dnl a test program.
AC_DEFUN(SC_C_BIGENDIAN,
[AC_CHECK_HEADERS(fp.h endian.h machine/endian.h sys/endian.h sys/machine.h)
AC_CACHE_CHECK(whether byte ordering is bigendian, sc_cv_c_bigendian,
[sc_cv_c_bigendian=unknown
# See if sys/param.h defines the BYTE_ORDER macro.
AC_TRY_COMPILE([#include "confdefs.h"
#ifdef HAVE_FP_H
#include <fp.h>
#endif
#ifdef HAVE_ENDIAN_H
#include <endian.h>
#endif
#ifdef HAVE_MACHINE_ENDIAN_H
#include <machine/endian.h>
#endif
#ifdef HAVE_SYS_ENDIAN_H
#include <sys/endian.h>
#endif
#ifdef HAVE_SYS_MACHINE_H
#include <sys/machine.h>
#endif
#include <sys/types.h>
#include <sys/param.h>], [
#if !BYTE_ORDER || !BIG_ENDIAN || !LITTLE_ENDIAN
 bogus endian macros
#endif], [# It does; now see whether it defined to BIG_ENDIAN or not.
AC_TRY_COMPILE([#include "confdefs.h"
#ifdef HAVE_FP_H
#include <fp.h>
#endif
#ifdef HAVE_MP_H
#include <mp.h>
#endif
#ifdef HAVE_ENDIAN_H
#include <endian.h>
#endif
#ifdef HAVE_MACHINE_ENDIAN_H
#include <machine/endian.h>
#endif
#ifdef HAVE_SYS_ENDIAN_H
#include <sys/endian.h>
#endif
#ifdef HAVE_SYS_MACHINE_H
#include <sys/machine.h>
#endif
#include <sys/types.h>
#include <sys/param.h>], [
#if BYTE_ORDER != BIG_ENDIAN
 not big endian
#endif], sc_cv_c_bigendian=yes, sc_cv_c_bigendian=no)])
if test $sc_cv_c_bigendian = unknown; then
AC_TRY_RUN([main () {
  /* Are we little or big endian?  From Harbison&Steele.  */
  union
  {
    long l;
    char c[sizeof (long)];
  } u;
  u.l = 1;
  exit (u.c[sizeof (long) - 1] == 1);
}], sc_cv_c_bigendian=no, sc_cv_c_bigendian=yes,
AC_MSG_ERROR([Could not determine endianness and cross compiling])
)
fi])
if test $sc_cv_c_bigendian = yes; then
  AC_DEFINE(WORDS_BIGENDIAN)
fi
])

SC_C_BIGENDIAN

dnl --------- Checks for libraries. ---------
dnl -- libpthread should be linked in last.
dnl -- Check on pthread_join since pthread_create, but not pthread_join,
dnl -- seems to be in libc on IRIX and we must generate a -lpthread in LIBS.
if test "(" X$PARALLEL = Xyes -a X$THREADS != Xno ")" -o X$THREADS = Xyes; then

AC_MSG_CHECKING([pthreads])
dnl see if posix threads are automatically linked ...
AC_LANG_SAVE
AC_LANG_CPLUSPLUS
LIBSSAV="$LIBS"
AC_TRY_LINK([#include <pthread.h>],[pthread_join(0,0);],[
HAVE_PTHREAD=yes],[
HAVE_PTHREAD=no])
AC_LANG_RESTORE

dnl see if posix threads are in -lpthread
if test $HAVE_PTHREAD = no; then
AC_LANG_SAVE
AC_LANG_CPLUSPLUS
LIBSSAV="$LIBS"
LIBS="$LIBS -lpthread"
AC_TRY_LINK([#include <pthread.h>],[pthread_join(0,0);],[
HAVE_PTHREAD=yes],[
HAVE_PTHREAD=no
LIBS="$LIBSSAV"])
AC_LANG_RESTORE
fi

dnl see if posix threads are in -lpthreads
if test $HAVE_PTHREAD = no; then
AC_LANG_SAVE
AC_LANG_CPLUSPLUS
LIBSSAV="$LIBS"
LIBS="$LIBS -lpthreads"
AC_TRY_LINK([#include <pthread.h>],[pthread_join(0,0);],[
HAVE_PTHREAD=yes],[
HAVE_PTHREAD=no
LIBS="$LIBSSAV"])
AC_LANG_RESTORE
fi

AC_MSG_RESULT($HAVE_PTHREAD)
fi
if test X$HAVE_PTHREAD = Xyes; then
  AC_DEFINE(HAVE_PTHREAD)
  EXTRADEFINES="-D_REENTRANT $EXTRADEFINES"
  AC_CHECK_FUNC(pthread_attr_getstacksize)
  AC_CHECK_FUNC(pthread_attr_setstacksize)
fi

AC_CHECK_LIB(dl,exit)
AC_CHECK_LIB(sun,exit)
AC_CHECK_LIB(m,exit)
AC_CHECK_LIB(fl,exit)
AC_CHECK_HEADER(perf.h,[
  AC_CHECK_LIB(perf,perf_set_config,[
    HAVE_PERF=yes
    LIBS="-lperf $LIBS"
    AC_DEFINE(HAVE_PERF)
    ])
  ]
)

if test X$PARALLEL = Xyes; then

  dnl ----- check for the mpi library
  AC_LANG_SAVE
  AC_LANG_CPLUSPLUS
  dnl Must use try_link since check_header runs cpp which doesn't
  dnl always find mpi.h (if mpicc and no -I is used for example)
  AC_TRY_LINK([#include <mpi.h>],[],
    AC_CHECK_FUNC(MPI_Init,HAVE_MPI=yes,[
      AC_CHECK_LIB(mpi,MPI_Init,[HAVE_MPI=yes;LIBS="-lmpi $LIBS"],[
        AC_CHECK_LIB(mpich,MPI_Init,[HAVE_MPI=yes;LIBS="-lmpich $LIBS"])
        ]
      )]
    )
  )
  if test X$HAVE_MPI = Xyes; then
    AC_DEFINE(HAVE_MPI)
    dnl ----- check for functions that might be in an mpi library
    AC_CHECK_FUNC(mpc_rcvncall,[HAVE_MPL=yes;AC_DEFINE(HAVE_MPL)])
    AC_CHECK_FUNC(p4_initenv,[HAVE_P4=yes;AC_DEFINE(HAVE_P4)])
    AC_CHECK_FUNC(MPI2_RMA_init,
                 [HAVE_PUMA_MPI2=yes;AC_DEFINE(HAVE_PUMA_MPI2)])
    AC_CHECK_FUNC(MPI_Win_create,
                 [HAVE_MPI2_ONE_SIDED=yes;AC_DEFINE(HAVE_MPI2_ONE_SIDED)])
  fi
  AC_LANG_RESTORE

  dnl ----- check for the pvm library
  AC_CHECK_HEADER(pvm.h,[
    AC_CHECK_LIB(pvm,exit,HAVE_PVM=yes)
    ]
  )
  if test X$HAVE_PVM = Xyes; then
    AC_DEFINE(HAVE_PVM)
    LIBS="-lpvm $LIBS"
  fi

fi

AC_SUBST(HAVE_PERF)
AC_SUBST(HAVE_MPL)
AC_SUBST(HAVE_P4)
AC_SUBST(HAVE_MPI2_ONE_SIDED)
AC_SUBST(HAVE_MPI)
AC_SUBST(HAVE_PVM)
AC_SUBST(HAVE_PTHREAD)
AC_SUBST(EXTRADEFINES)

dnl ---------- Checks for header files. -----------

AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h sys/ioctl.h sys/time.h unistd.h pwd.h)
AC_CHECK_HEADERS(sys/times.h sys/resource.h time.h machine/fpu.h asm/fpu.h)
AC_CHECK_HEADERS(termios.h sys/stat.h sys/types.h dlfcn.h stdint.h)

dnl -- Checks for typedefs, structures, and compiler characteristics. --
AC_C_CONST
AC_TYPE_SIZE_T
AC_STRUCT_TM

dnl --------- Checks for library functions. ---------
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strerror sigfillset signal system getpwuid geteuid)
AC_CHECK_FUNCS(gethostname time ctime ieee_set_fp_control ieee_get_fp_control)
AC_CHECK_FUNCS(setrlimit)

dnl --------- Checks for a C++ linkable fchdir. ---------
AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING([fchdir])
AC_TRY_COMPILE([#include <unistd.h>],[
fchdir(0);
],[HAVE_FCHDIR=yes
AC_DEFINE(HAVE_FCHDIR)
AC_MSG_RESULT(yes)],[
AC_MSG_RESULT(no)
HAVE_FCHDIR=no
])
AC_LANG_RESTORE
AC_SUBST(HAVE_FCHDIR)

dnl --------- Check for ios::fmtflags. ---------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING([ios::fmtflags])
AC_TRY_COMPILE([#include <$iostream>],[
$NAMESPACE_STD ios::fmtflags flags;
],[HAVE_IOS_FMTFLAGS=yes
AC_DEFINE(HAVE_IOS_FMTFLAGS)
AC_MSG_RESULT(yes)],[
AC_MSG_RESULT(no)
HAVE_IOS_FMTFLAGS=no
])
AC_LANG_RESTORE
AC_SUBST(HAVE_IOS_FMTFLAGS)

dnl --------- check for long long  ---------

if test X"$LONGLONG" = Xyes; then
AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING(long long)
AC_TRY_COMPILE([],[
long long i;
],[AC_DEFINE(HAVE_LONG_LONG)
AC_MSG_RESULT(yes)],[
AC_MSG_RESULT(no)
])
AC_LANG_RESTORE
fi

dnl --------- Check for sgetn. ---------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING([sgetn])
AC_TRY_COMPILE([#include <$iostream>],[
char *c=0;
$NAMESPACE_STD streambuf *s=0; s->sgetn(c,0);
],[HAVE_SGETN=yes
AC_DEFINE(HAVE_SGETN)
AC_MSG_RESULT(yes)],[
AC_MSG_RESULT(no)
HAVE_SGETN=no
])
AC_LANG_RESTORE
AC_SUBST(HAVE_SGETN)

dnl --------- Check for pubseekoff. ---------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING([pubseekoff])
AC_TRY_COMPILE([#include <$iostream>],[
$NAMESPACE_STD cout.rdbuf()->pubseekoff(0,$NAMESPACE_STD ios::beg,
                                        $NAMESPACE_STD ios::out);
],[HAVE_PUBSEEKOFF=yes
AC_DEFINE(HAVE_PUBSEEKOFF)
AC_MSG_RESULT(yes)],[
AC_MSG_RESULT(no)
HAVE_PUBSEEKOFF=no
])
AC_LANG_RESTORE
AC_SUBST(HAVE_PUBSEEKOFF)

dnl --------- Check for seekoff. ---------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING([seekoff])
AC_TRY_COMPILE([#include <$iostream>],[
$NAMESPACE_STD cout.rdbuf()->seekoff(0,$NAMESPACE_STD ios::beg,
                                     $NAMESPACE_STD ios::out);
],[HAVE_SEEKOFF=yes
AC_DEFINE(HAVE_SEEKOFF)
AC_MSG_RESULT(yes)],[
AC_MSG_RESULT(no)
HAVE_SEEKOFF=no
])
AC_LANG_RESTORE
AC_SUBST(HAVE_SEEKOFF)

dnl --------- Check for signal handler argument lists. ---------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING([signal handler needs ellipsis])
AC_TRY_LINK([
#include <signal.h>
extern "C" {
  typedef void (*signal_handler)(...);
  void handler(int);
}
void handler(int) {}
],[
  signal(SIGINT, (signal_handler)handler);
],
AC_DEFINE(SIGHASELLIP) AC_MSG_RESULT(yes),
AC_MSG_RESULT(no))
AC_LANG_RESTORE

dnl --------- Checks for rand functions ---------

AC_CHECK_FUNCS(drand48)

dnl --------- Checks for SYSV IPC. ---------

AC_CHECK_FUNCS(shmget semget)
AC_CHECK_HEADERS(sys/ipc.h sys/sem.h sys/shm.h)
AC_MSG_CHECKING([for SYSV IPC])
if test X$ac_cv_func_shmget = Xyes -a \
        X$ac_cv_func_semget = Xyes -a \
        X$ac_cv_header_sys_ipc_h = Xyes -a \
        X$ac_cv_header_sys_sem_h = Xyes -a \
        X$ac_cv_header_sys_shm_h = Xyes; then
  HAVE_SYSV_IPC=yes
  AC_DEFINE(HAVE_SYSV_IPC)
else
  HAVE_SYSV_IPC=no
fi
AC_MSG_RESULT($HAVE_SYSV_IPC)
AC_SUBST(HAVE_SYSV_IPC)

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING([if semctl requires semun])
AC_TRY_LINK([
#include <sys/types.h>
#include <sys/ipc.h>
#include <sys/sem.h>
],[
  int val = 0;
  semctl(0,0,0,val);
],[
case $target in
  *-sgi-irix*)
    AC_DEFINE(SEMCTL_REQUIRES_SEMUN) AC_MSG_RESULT(sgi-irix -- yes)
  ;;
  *)
    AC_MSG_RESULT(no)
  ;;
esac
],
AC_DEFINE(SEMCTL_REQUIRES_SEMUN) AC_MSG_RESULT(yes))
AC_LANG_RESTORE

dnl ----------- Fortran symbol names --------------

AC_MSG_CHECKING(fortran symbols)
if test -n "$F77" -a "$F77" != no ; then
  /bin/rm -f ffunc.f flink.c
  echo "      subroutine ffunc()" > ffunc.f
  echo "      return" >> ffunc.f
  echo "      end" >> ffunc.f
  $F77 -c ffunc.f 1>/dev/null 2>/dev/null
  echo "main(){ FF(); return 0; }" > flink.c
  if $CC -o flink -DFF=ffunc flink.c ffunc.o $LDFLAGS $LIBS 1>/dev/null 2>/dev/null; then
    AC_MSG_RESULT(same as C)
    F77_SYMBOLS=symbol
  elif $CC -o flink -DFF=ffunc_ flink.c ffunc.o $LDFLAGS $LIBS 1>/dev/null 2>/dev/null; then
    AC_MSG_RESULT(lowercase with underscore)
    F77_SYMBOLS=symbol_
  elif $CC -o flink -DFF=FFUNC flink.c ffunc.o $LDFLAGS $LIBS 1>/dev/null 2>/dev/null; then
    AC_MSG_RESULT(uppercase)
    F77_SYMBOLS=SYMBOL
  elif $CC -o flink -DFF=FFUNC_ flink.c ffunc.o $LDFLAGS $LIBS 1>/dev/null 2>/dev/null; then
    AC_MSG_RESULT(uppercase with underscore)
    F77_SYMBOLS=SYMBOL_
  else
    AC_MSG_RESULT(giving up)
    AC_MSG_ERROR(could not determine F77 symbol names)
  fi
  /bin/rm -f ffunc.f ffunc.o flink flink.c flink.o ffunc
else
  F77_SYMBOLS=symbol_
  AC_MSG_RESULT(guessing lowercase with underscore)
fi
AC_SUBST(F77_SYMBOLS)

dnl ----------- check for FORTRAN libraries --------------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
LIBSSAV="$LIBS"
LIBS="$LIBSSAV $FLIBS"

LIBBLAS=""
F77_DGEMM=`$PERL $srcdir/bin/mkf77sym.pl.in -method $F77_SYMBOLS DAXPY`
AC_CHECK_FUNC($F77_DGEMM,HAVE_BLAS=yes,[
  AC_CHECK_LIB(essl,$F77_DGEMM,[HAVE_BLAS=yes;LIBBLAS="-lessl"],
    AC_CHECK_LIB(blas,$F77_DGEMM,[HAVE_BLAS=yes;LIBBLAS="-lblas"])
  )]
)
AC_SUBST(HAVE_BLAS)
if test X$HAVE_BLAS != Xyes; then
  echo "WARNING: Could not link to the BLAS library.  It can be obtained at"
  echo "http://www.netlib.org/blas.  Use --with-libdir and/or --with-libs"
  echo "to specify the name of the library."
  AC_MSG_ERROR([BLAS is required to complete the build])
fi

LIBS="$LIBSSAV $LIBBLAS $FLIBS"

LIBLAPACK=""
F77_DGESVD=`$PERL $srcdir/bin/mkf77sym.pl.in -method $F77_SYMBOLS DGESVD`
AC_CHECK_FUNC($F77_DGESVD,HAVE_LAPACK=yes,[
  AC_CHECK_LIB(lapack,$F77_DGESVD,[HAVE_LAPACK=yes;LIBLAPACK="-llapack"]
  )]
)
AC_SUBST(HAVE_LAPACK)
if test X$HAVE_LAPACK != Xyes; then
  echo "Could not link to the LAPACK library.  It can be obtained at"
  echo "http://www.netlib.org/lapack.  Use --with-libdir and/or --with-libs"
  echo "to specify the name of the library."
  AC_MSG_ERROR([LAPACK is required to complete the build])
fi

LIBS="$LIBSSAV $LIBLAPACK $LIBBLAS"
AC_LANG_RESTORE

dnl ----------- check for Scalable BLAS library --------------

HAVE_SCALABLE_BLAS=no
AC_CHECK_FUNC(sB_init,
  HAVE_SCALABLE_BLAS=yes,[
  AC_CHECK_LIB(sB_BLAS,sB_init,[
    HAVE_SCALABLE_BLAS=yes
    LIBS="-lsB_BLAS $LIBS"
  ])]
)
if test $HAVE_SCALABLE_BLAS = yes; then
  AC_DEFINE(HAVE_SCALABLE_BLAS)
fi

dnl ----------- NIAMA library checks --------------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_CHECK_PROG(NIAMACFG,niama-config,niama-config)
if test X$NIAMACFG != X; then
  LIBSSAV="$LIBS"
  CPPSAV="$CPPFLAGS"
  NIAMALIBS="`niama-config --libs` `niama-config --corbalibs`"
  CPPFLAGS="$CPPFLAGS `niama-config --cppflags`"
  LIBS="$NIAMALIBS $LIBS"
AC_TRY_LINK([
#include <niama.h>
],[
  exit(0);
],[
  AC_DEFINE(HAVE_NIAMA)
],[
  LIBS="$LIBSSAV"
  CPPFLAGS="$CPPSAV"
]
)
fi
AC_LANG_RESTORE

dnl --------- Checks that must be done last. ---------

AC_MSG_CHECKING([for special flags that must be set last])
case $target in
  alpha*)
    if test ! X$GXX = Xyes; then
      CXXFLAGS="$CXXFLAGS -ptr \$(TOPDIR)/cxx_tmpl_repo"
      LDFLAGS="$LDFLAGS -ptr \$(TOPDIR)/cxx_tmpl_repo"
      AC_MSG_RESULT("alpha* non-GNU C++")
    fi
  ;;
  *-dec-osf*)
    if test ! X$GXX = Xyes; then
      CXXFLAGS="$CXXFLAGS -ptr \$(TOPDIR)/cxx_tmpl_repo"
      LDFLAGS="$LDFLAGS -ptr \$(TOPDIR)/cxx_tmpl_repo"
      AC_MSG_RESULT("*-dec-osf* non-GNU C++")
    fi
  ;;
  *-solaris*)
    if test ! X$GXX = Xyes; then
      CXXFLAGS="$CXXFLAGS -instances=explicit"
      AC_DEFINE(EXPLICIT_TEMPLATE_INSTANTIATION)
      AC_MSG_RESULT("*-solaris*")
    fi
  ;;
  i860-intel-puma*)
    LDFLAGS="$LDFLAGS -mpuma -mnoieee"
    AC_MSG_RESULT(paragon puma)
  ;;
  i686-intel-cougar*)
    LDFLAGS="$LDFLAGS -mcougar -mnoieee"
    AC_MSG_RESULT(teraflop cougar)
  ;;
  i860-intel-sunmos*)
    LDFLAGS="$LDFLAGS -msunmos -mnoieee"
    AC_MSG_RESULT(paragon sunmos)
  ;;
  i860-intel-osf*)
    LDFLAGS="$LDFLAGS -mnx -mnoieee"
    AC_MSG_RESULT(osf paragon)
  ;;
changequote(<<, >>)dnl
  mips*-sgi-irix6.[01]*)
changequote([, ])dnl
    if test "X$GXX" != Xyes; then
      CXX="\$(SRCTOPDIR)/bin/CCfix CC"
    fi
    AC_MSG_RESULT("mips*-sgi-irix6.0 or mips*-sgi-irix6.1")
  ;;
  rs6000-*)
    if test "X$CXX" = XmpCC -o "X$CXX" = XmpCC_r; then
      CXX="mpCC_r"
    fi
    if test "X$CXX" = XxlC; then
      CXX="xlC_r"
    fi
    AC_MSG_RESULT(rs6000)
  ;;
  *)
    AC_MSG_RESULT(none)
  ;;
esac

dnl --------- Shared library configuration. ---------

if test "$enableshared" = no; then
  enablelibtool=no
fi

if test "$enablelibtool" = yes; then

case "$target" in
NONE) lt_target="$host" ;;
*) lt_target="$target" ;;
esac

libtool_flags=
test "$enableshared" = no && libtool_flags="$libtool_flags --disable-shared"
test "$enablestatic" = no && libtool_flags="$libtool_flags --disable-static"

test ! -d bin || mkdir bin

CC="$CXX" CFLAGS="$CXXFLAGS" CPPFLAGS="$CPPFLAGS" \
LD="$LD" LDFLAGS="$LDFLAGS" LIBS="$LIBS $FLIBS" \
LN_S="$LN_S" NM="$NM" RANLIB="$RANLIB" \
DLLTOOL="$DLLTOOL" AS="$AS" OBJDUMP="$OBJDUMP" \
${CONFIG_SHELL-/bin/sh} $ac_aux_dir/ltconfig --no-reexec \
--output=bin/sc-libtool \
$libtool_flags --no-verify $ac_aux_dir/ltmain.sh $lt_target \
|| { echo "configure: error: libtool configure failed" 1>&2; exit 1; }

OBJSUF=lo
LIBSUF=la

else

enableshared=no

fi

ENABLESHARED=$enableshared
AC_SUBST(ENABLESHARED)

dnl --------- Find the list of all sc libraries. ---------

SCLIBS=`$PERL $srcdir/bin/listlibs.pl -I$srcdir/src/lib -DLIBSUF=$LIBSUF $srcdir/src/lib`
AC_SUBST(SCLIBS)

dnl --------- Defined the library directory macros. ---------

AC_DEFINE_UNQUOTED(SRC_SCLIBDIR, "$srcdir/lib")
AC_DEFINE_UNQUOTED(INSTALLED_SCLIBDIR, "$prefix/lib")

dnl --------- Create the stub Makefiles. ---------

$PERL $srcdir/bin/objectdir.pl $srcdir

AC_OUTPUT(lib/LocalMakefile bin/sc-config bin/mkf77sym.pl doc/doxygen.cfg)
chmod +x bin/sc-config
