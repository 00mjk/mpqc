dnl Process this file with autoconf to produce a configure script.
define([AC_CACHE_LOAD], )dnl for debugging configure.in
define([AC_CACHE_SAVE], )dnl for debugging configure.in
AC_INIT(src/lib/util/ref/ref.h)
AC_PREREQ(2.8)
AC_CONFIG_HEADER(src/lib/scconfig.h)
AC_CONFIG_AUX_DIR(bin)

AC_CANONICAL_SYSTEM

AC_DEFINE_UNQUOTED(HOST_ARCH, "$host")
AC_DEFINE_UNQUOTED(TARGET_ARCH, "$target")

dnl --------- Features ---------

AC_ARG_ENABLE(debug,
[  --enable-debug          Compile with debugging options],
[
case $enableval in
  yes)
    DEBUG=yes
  ;;
  no)
    DEBUG=no
  ;;
  opt)
    DEBUG=opt
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-debug ($enableval)])
  ;;
esac
],[
    DEBUG=no
]
)

AC_ARG_ENABLE(stl,
[  --enable-stl            Use the Standard Template Library.],
[
case $enableval in
  yes)
    AC_DEFINE(HAVE_STL)
  ;;
  no)
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-stl ($enableval)])
  ;;
esac
])

AC_ARG_ENABLE(parallel,
[  --enable-parallel       Compile for parallel execution, if possible.],
[
case $enableval in
  yes)
    PARALLEL=yes
  ;;
  no)
    PARALLEL=no
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-parallel ($enableval)])
  ;;
esac
],[
    PARALLEL=yes
]
)

AC_ARG_ENABLE(threads,
[  --enable-threads        Compile allowing use of threads, if possible.],
[
case $enableval in
  yes)
    THREADS=yes
  ;;
  no)
    THREADS=no
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-threads ($enableval)])
  ;;
esac
],[
    THREADS=no
]
)

AC_ARG_ENABLE(ref-debug,
[  --enable-ref-debug      Check for reference count overwrites/overflows/etc],
[
case $enableval in
  yes)
  ;;
  no)
    AC_DEFINE(REF_OPTIMIZE)
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-ref-debug ($enableval)])
  ;;
esac
],[
  if test $DEBUG = no -o $DEBUG = opt; then
    AC_DEFINE(REF_OPTIMIZE)
  fi
]
)

AC_ARG_ENABLE(ref-macros,
[  --disable-ref-macros    Do not use macros to declare ref classes.],
[
case $enableval in
  yes)
    AC_DEFINE(USE_REF_MACROS)
  ;;
  no)
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-ref-macros ($enableval)])
  ;;
esac
],[
  AC_DEFINE(USE_REF_MACROS)
]
)

AC_ARG_ENABLE(cross-compile,
[  --enable-cross-compile  Rather than checking, assume cross compilation.],
[
case $enableval in
  yes)
    cross_compiling=yes
  ;;
  no)
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-cross-compile ($enableval)])
  ;;
esac
])

AC_ARG_ENABLE(shared-libs,
[  --enable-shared-libs    Use shared libraries.],
[
case $enableval in
  yes)
    SHARED_LIBS=yes
  ;;
  no)
    SHARED_LIBS=no
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-shared-libs ($enableval)])
  ;;
esac
],[
SHARED_LIBS=no
]
)

AC_ARG_WITH(cc,
[  --with-cc               Gives the name of the C compiler to use.],
CC=$withval
)

AC_ARG_WITH(cxx,
[  --with-cxx              Gives the name of the C++ compiler to use.],
CXX=$withval
)

AC_ARG_WITH(f77,
[  --with-f77              Gives the name of the FORTRAN 77 compiler to use.],
F77=$withval
)

AC_ARG_WITH(ranlib,
[  --with-ranlib           Gives the name of the ranlib program.],
RANLIB=$withval
)

AC_ARG_WITH(ar,
[  --with-ar               Names the archive creator.],
AR=$withval
)

AC_ARG_WITH(ld,
[  --with-ld               Names the object linker.],
LD=$withval
)

AC_ARG_WITH(include,
[  --with-include          Specifies include directories (-Idir1 -Idir2).],
EXTRAINCLUDE=$withval
CPPFLAGS=$withval
echo Using extra include directories: $withval
)

AC_ARG_WITH(libs,
[  --with-libs             Specifies libraries (-llib1 -llib2).],
LIBS=$withval
echo Using extra libraries: $withval
)

LDFLAGS=
SH_LDFLAGS=
AC_ARG_WITH(libdirs,
[  --with-libdirs          Specifies library directories (-Ldir1 -Ldir2).],
LDFLAGS=$withval
SH_LDFLAGS=$withval
echo Using extra library directories: $withval
)

dnl --------- Want absolute path for srcdir, not relative. ---------

if test X$ac_srcdir_defaulted = Xyes; then
  case $srcdir in
    ../*)
      srcdir=`(cd $srcdir; pwd)`
      #srcdir=`(cd $srcdir; echo $PWD)`
      ;;
  esac
fi

dnl --------- Checks for programs. ---------
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_CC
dnl ac_prog_cxx's order isn't what i need
AC_CHECK_PROGS(CXX, g++ c++ gcc CC cxx xlC, gcc)
dnl sees if CXX is a GNU compiler
AC_PROG_CXX
AC_PROG_F77
AC_PROG_CPP
AC_PROG_CXXCPP
AC_CHECK_PROG(AR,ar,ar)
AC_CHECK_PROG(PERL,perl,perl)
AC_CHECK_PROG(BISON,bison,bison)
AC_CHECK_PROG(FLEX,flex,flex)

dnl The lack of certain tools is a show stopper

changequote(<<, >>)dnl

if [ X$BISON = X ]; then
  echo "Could not find the program bison.  It can be obtained at"
  echo "ftp://prep.ai.mit.edu/pub/gnu"
  exit 1
fi

if [ X$FLEX = X ]; then
  echo "Could not find the program flex.  It can be obtained at"
  echo "ftp://prep.ai.mit.edu/pub/gnu"
  exit 1
fi

if [ X$PERL = X ]; then
  echo "Could not find the program perl.  It can be obtained at"
  echo "ftp://prep.ai.mit.edu/pub/gnu"
  exit 1
fi

changequote([, ])dnl

dnl --------- This is at the top since its likely to cause problems. ---------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
CPPFLAGS="-I$srcdir/include -I$srcdir/src/lib $CPPFLAGS"
AC_CHECK_HEADER(FlexLexer.h,,[
AC_MSG_ERROR([couldn't find FlexLexer.h--try --with-include])
])
AC_LANG_RESTORE

dnl -------- Checks for cross compiling. ----------

# obsolete starting at autoconf 2.12
#if test X$cross_compiling != Xyes; then
#AC_C_CROSS
#fi

#if test X$cross_compiling = Xyes -a X$target = X$host; then
#  AC_MSG_ERROR([Cross compiling, but target is host (use --host).])
#fi

dnl -------- Checks for compiler/linker options. ----------

# options needed only for optimization
COPTIONS_OPT=-O
# options needed only for debugging
COPTIONS_DBG=-g
# options that are always needed
COPTIONS_MISC=

# options needed only for optimization
CXXOPTIONS_OPT=-O
# options needed only for debugging
CXXOPTIONS_DBG=-g
# options that are always needed
CXXOPTIONS_MISC=

OBJSUF=o
LIBSUF=a
CCDEPENDSUF=none
CXXDEPENDSUF=none
CCDEPENDFLAGS=-M
CXXDEPENDFLAGS=-M

SH_LD=ld
ELF=no
ELF_MAJOR=1
ELF_MINOR=0.1

dnl -- does machine use elf format --

changequote(<<, >>)dnl
if test X$SHARED_LIBS = Xyes; then
  case $target in
    mips*-sgi-irix[5-6]* | r*-sgi-irix6*)
      ELF=yes
      LIBSUF=so
      if test X$GXX = Xyes; then
        SH_LD="\$(CXX)"
        LDFLAGS="$LDFLAGS -Wl,-rpath -Wl,\$(LIBDIR)"
        SH_LDFLAGS="$SH_LDFLAGS -elf -shared \
          -Wl,-update_registry -Wl,\$(LIBDIR)/so_locations \
          -Wl,-rdata_shared \
          -Wl,-rpath -Wl,\$(LIBDIR) \
          -Wl,-soname -Wl,\$(TARGET_TO_MAKE).so.\$(ELF_MAJOR)"
      else
        LDFLAGS="$LDFLAGS -rpath \$(LIBDIR)"
        SH_LDFLAGS="$SH_LDFLAGS -elf -shared -rdata_shared -rpath \$(LIBDIR) \
                     -update_registry \$(LIBDIR)/so_locations \
                     -soname \$(TARGET_TO_MAKE).so.\$(ELF_MAJOR)"
      fi
    ;;
    *-linux)
      ELF=yes
      LIBSUF=so
      SH_LD="\$(CXX)"
      SH_LDFLAGS="$SH_LDFLAGS -shared -Wl,-soname -Wl,\$(TARGET_TO_MAKE).so.\$(ELF_MAJOR)"
      LDFLAGS="$LDFLAGS -Wl,-rpath -Wl,\$(LIBDIR)"
    ;;
  esac
fi
changequote([, ])dnl

if test X$ELF = Xyes; then
  if test X$GCC = Xyes; then
    COPTIONS_MISC="$COPTIONS_MISC -fPIC"
  else
    COPTIONS_MISC="$COPTIONS_MISC -KPIC"
  fi
  if test X$GXX = Xyes; then
    CXXOPTIONS_MISC="$CXXOPTIONS_MISC -fPIC"
  else
    CXXOPTIONS_MISC="$CXXOPTIONS_MISC -KPIC"
  fi
fi

dnl -- special optimization options --

AC_MSG_CHECKING([for special optimization options])
case $target in
  rs6000-ibm-aix3.2.* | rs6000-ibm-aix4.* | powerpc-ibm-aix4.*)
    if test X$GCC != Xyes; then
      COPTIONS_OPT="-O3"
    fi
    if test X$GXX != Xyes; then
      CXXOPTIONS_OPT="-O3"
    fi
    AC_MSG_RESULT("rs6000 or powerpc")
  ;;
  mips*-sgi-irix5*)
    if test X$GCC != Xyes; then
      COPTIONS_OPT="-O -Olimit 2000"
    fi
    AC_MSG_RESULT("mips-sgi-irix5")
  ;;
changequote(<<, >>)dnl
  mips4-sgi-irix6.[01]* | r8000-sgi-irix6.[01]* | r10000-sgi-irix6.[01]*)
changequote([, ])dnl
    if test X$GCC != Xyes; then
      COPTIONS_OPT="-O2 -TENV:use_fp \
        -OPT:const_copy_limit=20000:fold_arith_limit=20000:global_limit=20000"
    fi
    if test X$GXX != Xyes; then
      CXXOPTIONS_OPT="-O2 -TENV:use_fp \
        -OPT:const_copy_limit=20000:fold_arith_limit=20000:global_limit=20000"
      AC_DEFINE(NO_VIRTUAL_BASES)
    fi
    AC_MSG_RESULT("old mips4-sgi-irix r8000-sgi-irix or r10000-sgi-irix")
  ;;
  mips4-sgi-irix* | r8000-sgi-irix* | r10000-sgi-irix*)
    if test X$GCC != Xyes; then
      COPTIONS_OPT="-O3 -Ofast -OPT:const_copy_limit=20000"
    fi
    if test X$GXX != Xyes; then
      CXXOPTIONS_OPT="-O3 -Ofast -OPT:const_copy_limit=20000"
      AC_DEFINE(NO_VIRTUAL_BASES)
    fi
    AC_MSG_RESULT("mips4-sgi-irix r8000-sgi-irix or r10000-sgi-irix")
  ;;
changequote(<<, >>)dnl
  i[56]86-*)
changequote([, ])dnl
    if test X$GCC = Xyes; then
      COPTIONS_OPT="-O2"
    fi
    if test X$GXX = Xyes; then
      CXXOPTIONS_OPT="-O2"
    fi
    AC_MSG_RESULT("i586 or i686")
  ;;
  i860-intel-*)
    if test X$GCC != Xyes; then
      COPTIONS_OPT="-O3 -Knoieee"
    fi
    AC_MSG_RESULT("i860")
  ;;
  *)
    AC_MSG_RESULT("none")
  ;;
esac

dnl -- special architecture options --

AC_MSG_CHECKING([for special architecture options])
case $target in
  mips-*)
    COPTIONS_MISC="$COPTIONS_MISC -mips2"
    CXXOPTIONS_MISC="$CXXOPTIONS_MISC -mips2"
    AC_MSG_RESULT(mips)
  ;;
changequote(<<, >>)dnl
  mips[123]-*)
changequote([, ])dnl
    COPTIONS_MISC="$COPTIONS_MISC -$target_cpu"
    CXXOPTIONS_MISC="$CXXOPTIONS_MISC -$target_cpu"
    AC_MSG_RESULT($target_cpu)
  ;;
  mips4-*)
    if test X$GCC = Xyes; then
      COPTIONS_MISC="$COPTIONS_MISC -mips4"
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -mips4"
    else
      LDFLAGS="$LDFLAGS -64"
      COPTIONS_MISC="$COPTIONS_MISC -64 -mips4"
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -64 -mips4"
    fi
    AC_MSG_RESULT(mips4)
  ;;
  r8000-*)
    if test X$GCC = Xyes; then
      COPTIONS_MISC="$COPTIONS_MISC -mips4 -mcpu=r8000"
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -mips4 -mcpu=r8000"
    else
      LDFLAGS="$LDFLAGS -64 -G0"
      COPTIONS_MISC="$COPTIONS_MISC -64 -G0 -mips4 -TARG:processor=r8000"
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -64 -G0 -mips4 -TARG:processor=r8000"
    fi
    AC_MSG_RESULT(r8000)
  ;;
  r10000-*)
    if test X$GCC = Xyes; then
      COPTIONS_MISC="$COPTIONS_MISC -mips4 -mcpu=r8000"
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -mips4 -mcpu=r8000"
    else
      LDFLAGS="$LDFLAGS -64 -G0"
      COPTIONS_MISC="$COPTIONS_MISC -64 -G0 -mips4 -r10000"
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -64 -G0 -mips4 -r10000"
    fi
    AC_MSG_RESULT(r10000)
  ;;
  rs6000-* | powerpc-*)
    if test X$GCC != Xyes; then
      LDFLAGS="$LDFLAGS -bmaxdata:0x70000000"
    fi
    if test X$CC = XxlC -o X$CC = XxlC_r; then
      CCDEPENDSUF=u
    fi
    if test X$CXX = XxlC -o X$CXX = XxlC_r; then
      CXXDEPENDSUF=u
      CXXDEPENDFLAGS="-M -E"
    fi
    if test X$CC = Xmpcc -o X$CC = Xmpcc_r; then
      CCDEPENDSUF=u
    fi
    if test X$CXX = XmpCC -o X$CXX = XmpCC_r; then
      CXXDEPENDSUF=u
      CXXDEPENDFLAGS="-M -E"
    fi
    AC_MSG_RESULT(rs6000 or powerpc)
  ;;
  i686-intel-cougar*)
    COPTIONS_MISC="$COPTIONS_MISC -mcougar"
    CXXOPTIONS_MISC="$CXXOPTIONS_MISC -mcougar"
    EXTRADEFINES="-D_REENTRANT $EXTRADEFINES"
    if test X$cross_compiling = Xyes; then
      AR=tflop-ar
    fi
    AC_MSG_RESULT(teraflop cougar)
  ;;
changequote(<<, >>)dnl
  i[456]86-*)
changequote([, ])dnl
    if test X$GCC = Xyes; then
      COPTIONS_MISC="$COPTIONS_MISC -m486"
    fi
    if test X$GXX = Xyes; then
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -m486"
    fi
    AC_MSG_RESULT(intel)
  ;;
  i860-intel-puma*)
    if test X$GCC = Xyes; then
      COPTIONS_MISC="$COPTIONS_MISC -mpuma"
    else
      COPTIONS_MISC="$COPTIONS_MISC -D__PUMAGON__"
      COPTIONS_MISC="$COPTIONS_MISC -L$PARAGON_XDEV/paragon/lib-coff/puma"
      COPTIONS_MISC="$COPTIONS_MISC -YS,$PARAGON_XDEV/paragon/lib-coff/puma"
      COPTIONS_MISC="$COPTIONS_MISC -lpuma -lm -lkmath"
      COPTIONS_MISC="$COPTIONS_MISC $PARAGON_XDEV/paragon/lib-coff/puma/libstubs.o"
    fi
    if test X$GXX = Xyes; then
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -mpuma"
    fi
    if test X$cross_compiling = Xyes; then
      AR=ar860
    fi
    AC_MSG_RESULT(paragon puma)
  ;;
  i860-intel-sunmos*)
    if test X$GCC = Xyes; then
      COPTIONS_MISC="$COPTIONS_MISC -msunmos"
    else
      COPTIONS_MISC="$COPTIONS_MISC -DSUNMOS -D__PUMAGON__"
      COPTIONS_MISC="$COPTIONS_MISC -L$PARAGON_XDEV/paragon/lib-coff/sunmos"
      COPTIONS_MISC="$COPTIONS_MISC -Wl,-d0x4000000,-k"
      COPTIONS_MISC="$COPTIONS_MISC -YS,$PARAGON_XDEV/paragon/lib-coff/sunmos"
      COPTIONS_MISC="$COPTIONS_MISC -lm -lsunmos -lm"
    fi
    if test X$GXX = Xyes; then
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -msunmos"
    fi
    if test X$cross_compiling = Xyes; then
      AR=ar860
    fi
    AC_MSG_RESULT(paragon sunmos)
  ;;
  i860-intel-osf*)
    if test X$GCC = Xyes; then
      COPTIONS_MISC="$COPTIONS_MISC -mnx"
    fi
    if test X$GXX = Xyes; then
      CXXOPTIONS_MISC="$CXXOPTIONS_MISC -mnx"
    fi
    if test X$cross_compiling = Xyes; then
      AR=ar860
    fi
    AC_MSG_RESULT(paragon osf)
  ;;
  *)
    AC_MSG_RESULT("none")
  ;;
esac

dnl ----------- check for C++ typename ---------------
dnl This is done before template flags are set to avoid
dnl undefined symbol problems.

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING("checking for C++ typename keyword ... ")
AC_TRY_LINK([
  class X { public: typedef int t; X(){} };
  template <class T> void f(T i) {typename T::t x;}
],[
  X g;
  f(g);
],[
AC_DEFINE(HAVE_TYPENAME)
AC_MSG_RESULT("yes")
],
AC_MSG_RESULT("no")
);
AC_LANG_RESTORE

dnl -- other options --

AC_MSG_CHECKING([GNU c++ library/template flags])
if test X$GXX = Xyes; then
  CXXOPTIONS_MISC="$CXXOPTIONS_MISC -fno-implicit-templates"
  EXPLICIT_TEMPLATE_INSTANTIATION=yes
  AC_DEFINE(EXPLICIT_TEMPLATE_INSTANTIATION)
  AC_MSG_RESULT("GNU")
else
changequote(<<, >>)dnl
  case $target in
    *-sgi-irix6.0*)
      AC_MSG_RESULT("*-sgi-irix6.0*")
    ;;
    *-sgi-irix*)
      if test X$CXX = XCC; then
        CXXOPTIONS_MISC="$CXXOPTIONS_MISC -ptused"
      fi
      AC_MSG_RESULT("*-sgi-irix*")
      ;;
    *)
      AC_MSG_RESULT("generic non GNU")
      ;;
    esac
changequote([, ])dnl
fi

if test $DEBUG = yes; then
  CFLAGS="$COPTIONS_DBG $COPTIONS_MISC"
  CXXFLAGS="$CXXOPTIONS_DBG $CXXOPTIONS_MISC"
  LDFLAGS="$LDFLAGS -g"
elif test $DEBUG = opt; then
  CFLAGS="$COPTIONS_DBG $COPTIONS_OPT $COPTIONS_MISC"
  CXXFLAGS="$CXXOPTIONS_DBG $COPTIONS_OPT $CXXOPTIONS_MISC"
  LDFLAGS="$LDFLAGS -g"
else
  CFLAGS="$COPTIONS_OPT $COPTIONS_MISC"
  CXXFLAGS="$CXXOPTIONS_OPT $CXXOPTIONS_MISC"
fi

AC_SUBST(EXTRAINCLUDE)

AC_SUBST(COPTIONS_OPT)
AC_SUBST(COPTIONS_DBG)
AC_SUBST(COPTIONS_MISC)
AC_SUBST(CXXOPTIONS_OPT)
AC_SUBST(CXXOPTIONS_DBG)
AC_SUBST(CXXOPTIONS_MISC)

AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)

AC_SUBST(LDFLAGS)
AC_SUBST(ELF)
AC_SUBST(ELF_MAJOR)
AC_SUBST(ELF_MINOR)
AC_SUBST(SH_LD)
AC_SUBST(SH_LDFLAGS)

AC_SUBST(OBJSUF)
AC_SUBST(LIBSUF)
AC_SUBST(CCDEPENDSUF)
AC_SUBST(CXXDEPENDSUF)
AC_SUBST(CCDEPENDFLAGS)
AC_SUBST(CXXDEPENDFLAGS)

dnl -------- Checks for architecture specific features. --------
dnl (Doesn't work for cross compilation.)
dnl AC_CHECK_SIZEOF(void*)

dnl This version works on more machines without the need to run
dnl a test program.
AC_DEFUN(SC_C_BIGENDIAN,
[AC_CHECK_HEADERS(fp.h endian.h machine/endian.h sys/endian.h sys/machine.h)
AC_CACHE_CHECK(whether byte ordering is bigendian, sc_cv_c_bigendian,
[sc_cv_c_bigendian=unknown
# See if sys/param.h defines the BYTE_ORDER macro.
AC_TRY_COMPILE([#include "confdefs.h"
#ifdef HAVE_FP_H
#include <fp.h>
#endif
#ifdef HAVE_ENDIAN_H
#include <endian.h>
#endif
#ifdef HAVE_MACHINE_ENDIAN_H
#include <machine/endian.h>
#endif
#ifdef HAVE_SYS_ENDIAN_H
#include <sys/endian.h>
#endif
#ifdef HAVE_SYS_MACHINE_H
#include <sys/machine.h>
#endif
#include <sys/types.h>
#include <sys/param.h>], [
#if !BYTE_ORDER || !BIG_ENDIAN || !LITTLE_ENDIAN
 bogus endian macros
#endif], [# It does; now see whether it defined to BIG_ENDIAN or not.
AC_TRY_COMPILE([#include "confdefs.h"
#ifdef HAVE_FP_H
#include <fp.h>
#endif
#ifdef HAVE_MP_H
#include <mp.h>
#endif
#ifdef HAVE_ENDIAN_H
#include <endian.h>
#endif
#ifdef HAVE_MACHINE_ENDIAN_H
#include <machine/endian.h>
#endif
#ifdef HAVE_SYS_ENDIAN_H
#include <sys/endian.h>
#endif
#ifdef HAVE_SYS_MACHINE_H
#include <sys/machine.h>
#endif
#include <sys/types.h>
#include <sys/param.h>], [
#if BYTE_ORDER != BIG_ENDIAN
 not big endian
#endif], sc_cv_c_bigendian=yes, sc_cv_c_bigendian=no)])
if test $sc_cv_c_bigendian = unknown; then
AC_TRY_RUN([main () {
  /* Are we little or big endian?  From Harbison&Steele.  */
  union
  {
    long l;
    char c[sizeof (long)];
  } u;
  u.l = 1;
  exit (u.c[sizeof (long) - 1] == 1);
}], sc_cv_c_bigendian=no, sc_cv_c_bigendian=yes,
AC_MSG_ERROR([Could not determine endianness and cross compiling])
)
fi])
if test $sc_cv_c_bigendian = yes; then
  AC_DEFINE(WORDS_BIGENDIAN)
fi
])

SC_C_BIGENDIAN

dnl --------- Checks for libraries. ---------
dnl -- libpthread should be linked in last
if test "(" X$PARALLEL = Xyes -a X$THREADS != Xno ")" -o X$THREADS = Xyes; then
  dnl see if posix threads are automatically linked ...
  AC_CHECK_HEADER(pthread.h,[
    AC_CHECK_FUNC(pthread_create,HAVE_PTHREAD=yes,[
      dnl ... otherwise see if posix threads are in -lpthread ...
      AC_CHECK_LIB(pthread,pthread_create,[
        HAVE_PTHREAD=yes;LIBS="-lpthread $LIBS"
        ],[
        dnl ... otherwise see if posix threads are in -lpthreads
        AC_CHECK_LIB(pthreads,pthread_create,
                     [HAVE_PTHREAD=yes;LIBS="-lpthreads $LIBS"])
        ]
      )
      ]
    )
    ]
  )
fi
if test X$HAVE_PTHREAD = Xyes; then
  AC_DEFINE(HAVE_PTHREAD)
  EXTRADEFINES="-D_REENTRANT $EXTRADEFINES"
  AC_CHECK_FUNC(pthread_attr_getstacksize)
  AC_CHECK_FUNC(pthread_attr_setstacksize)
fi

AC_CHECK_LIB(dl,exit)
AC_CHECK_LIB(sun,exit)
AC_CHECK_LIB(m,exit)
AC_CHECK_LIB(fl,exit)
AC_CHECK_HEADER(perf.h,[
  AC_CHECK_LIB(perf,perf_set_config,[
    HAVE_PERF=yes
    LIBS="-lperf $LIBS"
    AC_DEFINE(HAVE_PERF)
    ])
  ]
)

if test X$PARALLEL = Xyes; then

  dnl ----- check for the mpi library
  dnl Must use try_link since check_header runs cpp which doesn't
  dnl always find mpi.h (if mpicc and no -I is used for example)
  AC_TRY_LINK([#include <mpi.h>],[],
    AC_CHECK_FUNC(MPI_Init,HAVE_MPI=yes,[
      AC_CHECK_LIB(mpi,MPI_Init,[HAVE_MPI=yes;LIBS="-lmpi $LIBS"],[
        AC_CHECK_LIB(mpich,MPI_Init,[HAVE_MPI=yes;LIBS="-lmpich $LIBS"])
        ]
      )]
    )
  )
  if test X$HAVE_MPI = Xyes; then
    AC_DEFINE(HAVE_MPI)
    dnl ----- check for functions that might be in an mpi library
    AC_CHECK_FUNC(mpc_rcvncall,[HAVE_MPL=yes;AC_DEFINE(HAVE_MPL)])
    AC_CHECK_FUNC(p4_initenv,[HAVE_P4=yes;AC_DEFINE(HAVE_P4)])
    AC_CHECK_FUNC(MPI2_RMA_init,
                 [HAVE_PUMA_MPI2=yes;AC_DEFINE(HAVE_PUMA_MPI2)])
    AC_CHECK_FUNC(MPI_Win_create,
                 [HAVE_MPI2_ONE_SIDED=yes;AC_DEFINE(HAVE_MPI2_ONE_SIDED)])
  fi

  dnl ----- check for the pvm library
  AC_CHECK_HEADER(pvm.h,[
    AC_CHECK_LIB(pvm,exit,HAVE_PVM=yes)
    ]
  )
  if test X$HAVE_PVM = Xyes; then
    AC_DEFINE(HAVE_PVM)
    LIBS="-lpvm $LIBS"
  fi

  dnl ----- check for the intel nx library
  AC_CHECK_HEADER(nx.h,[
    AC_CHECK_LIB(nx,hrecv,[
      HAVE_HRECV=yes
      HAVE_NX=yes
      ],[
      AC_CHECK_LIB(nx,exit,HAVE_NX=yes)
      ]
    )
    ]
  )
  if test X$HAVE_NX = Xyes; then
    AC_DEFINE(HAVE_NX)
    LIBS="-lnx $LIBS"
  fi
  if test X$HAVE_HRECV = Xyes; then
    AC_DEFINE(HAVE_HRECV)
  fi

fi

AC_CHECK_LIB(blas,exit)
AC_CHECK_LIB(dgemm,exit)
AC_CHECK_LIB(sB_BLAS,sB_init,[
  HAVE_SCALABLE_BLAS=yes
  LIBS="-lsB_BLAS $LIBS"
  AC_DEFINE(HAVE_SCALABLE_BLAS)
])

AC_SUBST(HAVE_PERF)
AC_SUBST(HAVE_MPL)
AC_SUBST(HAVE_P4)
AC_SUBST(HAVE_PUMA_MPI2)
AC_SUBST(HAVE_MPI2_ONE_SIDED)
AC_SUBST(HAVE_MPI)
AC_SUBST(HAVE_PVM)
AC_SUBST(HAVE_NX)
AC_SUBST(HAVE_HRECV)
AC_SUBST(HAVE_PTHREAD)
AC_SUBST(EXTRADEFINES)

dnl ---------- Checks for header files. -----------

AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h sys/ioctl.h sys/time.h unistd.h pwd.h)
AC_CHECK_HEADERS(sys/times.h sys/resource.h)
AC_CHECK_HEADERS(termios.h sys/stat.h sys/types.h dlfcn.h)

dnl -- Checks for typedefs, structures, and compiler characteristics. --
AC_C_CONST
AC_TYPE_SIZE_T
AC_STRUCT_TM

dnl --------- Checks for library functions. ---------
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strerror sigfillset signal system fchdir getpwuid geteuid)

dnl --------- Check for ios::fmtflags. ---------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING([ios::fmtflags])
AC_TRY_COMPILE([#include <iostream.h>],[
ios::fmtflags flags;
],[HAVE_IOS_FMTFLAGS=yes
AC_DEFINE(HAVE_IOS_FMTFLAGS)
AC_MSG_RESULT(yes)],[
AC_MSG_RESULT(no)
HAVE_IOS_FMTFLAGS=no
])
AC_LANG_RESTORE
AC_SUBST(HAVE_IOS_FMTFLAGS)

dnl --------- check for long long  ---------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING(long long)
AC_TRY_COMPILE([],[
long long i;
],[AC_DEFINE(HAVE_LONG_LONG)
AC_MSG_RESULT(yes)],[
AC_MSG_RESULT(no)
])
AC_LANG_RESTORE

dnl --------- Check for pubseekoff. ---------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING([pubseekoff])
AC_TRY_COMPILE([#include <iostream.h>],[
cout.rdbuf()->pubseekoff(0,ios::beg,ios::out);
],[HAVE_PUBSEEKOFF=yes
AC_DEFINE(HAVE_PUBSEEKOFF)
AC_MSG_RESULT(yes)],[
AC_MSG_RESULT(no)
HAVE_PUBSEEKOFF=no
])
AC_LANG_RESTORE
AC_SUBST(HAVE_PUBSEEKOFF)

dnl --------- Check for seekoff. ---------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING([seekoff])
AC_TRY_COMPILE([#include <iostream.h>],[
cout.rdbuf()->seekoff(0,ios::beg,ios::out);
],[HAVE_SEEKOFF=yes
AC_DEFINE(HAVE_SEEKOFF)
AC_MSG_RESULT(yes)],[
AC_MSG_RESULT(no)
HAVE_SEEKOFF=no
])
AC_LANG_RESTORE
AC_SUBST(HAVE_SEEKOFF)

dnl --------- Check for signal handler argument lists. ---------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING([signal handler needs ellipsis])
AC_TRY_LINK([
#include <signal.h>
extern "C" {
  typedef void (*signal_handler)(...);
  void handler(int);
}
void handler(int) {}
],[
  signal(SIGINT, (signal_handler)handler);
],
AC_DEFINE(SIGHASELLIP) AC_MSG_RESULT(yes),
AC_MSG_RESULT(no))
AC_LANG_RESTORE

dnl --------- Checks for rand functions ---------

AC_CHECK_FUNCS(drand48)

dnl --------- Checks for SYSV IPC. ---------

AC_CHECK_FUNCS(shmget semget)
AC_CHECK_HEADERS(sys/ipc.h sys/sem.h sys/shm.h)
AC_MSG_CHECKING([for SYSV IPC])
if test X$ac_cv_func_shmget = Xyes -a \
        X$ac_cv_func_semget = Xyes -a \
        X$ac_cv_header_sys_ipc_h = Xyes -a \
        X$ac_cv_header_sys_sem_h = Xyes -a \
        X$ac_cv_header_sys_shm_h = Xyes; then
  HAVE_SYSV_IPC=yes
  AC_DEFINE(HAVE_SYSV_IPC)
else
  HAVE_SYSV_IPC=no
fi
AC_MSG_RESULT($HAVE_SYSV_IPC)
AC_SUBST(HAVE_SYSV_IPC)

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING([if semctl requires semun])
AC_TRY_LINK([
#include <sys/types.h>
#include <sys/ipc.h>
#include <sys/sem.h>
],[
  int val = 0;
  semctl(0,0,0,val);
],[
case $target in
  *-sgi-irix*)
    AC_DEFINE(SEMCTL_REQUIRES_SEMUN) AC_MSG_RESULT(sgi-irix -- yes)
  ;;
  *)
    AC_MSG_RESULT(no)
  ;;
esac
],
AC_DEFINE(SEMCTL_REQUIRES_SEMUN) AC_MSG_RESULT(yes))
AC_LANG_RESTORE

dnl --------- Checks for working mmap. ---------

AC_MSG_CHECKING([for Alpha mmap])
case $target in
  alpha-dec-osf*)
    HAVE_ALPHA_MMAP=yes
    AC_DEFINE(HAVE_ALPHA_MMAP)
    AC_MSG_RESULT(yes)
    ;;
  *)
    AC_MSG_RESULT(no)
    ;;
esac
AC_SUBST(HAVE_ALPHA_MMAP)

dnl ----------- Fortran symbol names --------------

if test -n $F77 ; then
  AC_MSG_CHECKING(fortran symbols... )
  /bin/rm -f ffunc.f flink.c
  echo "      subroutine ffunc()" > ffunc.f
  echo "      return" >> ffunc.f
  echo "      end" >> ffunc.f
  $F77 -c ffunc.f 1>/dev/null 2>/dev/null
  echo "main(){ FF(); return 0; }" > flink.c
  if $CC -o flink -DFF=ffunc flink.c ffunc.o 1>/dev/null 2>/dev/null; then
    AC_MSG_RESULT(same as C)
    AC_DEFINE_UNQUOTED(F77_DGEMM_SYMBOL,  dgemm)
  elif $CC -o flink -DFF=ffunc_ flink.c ffunc.o 1>/dev/null 2>/dev/null; then
    AC_MSG_RESULT(lowercase with underscore)
    AC_DEFINE_UNQUOTED(F77_DGEMM_SYMBOL,  dgemm_)
  elif $CC -o flink -DFF=FFUNC flink.c ffunc.o 1>/dev/null 2>/dev/null; then
    AC_MSG_RESULT(uppercase)
    AC_DEFINE_UNQUOTED(F77_DGEMM_SYMBOL,  DGEMM)
  elif $CC -o flink -DFF=FFUNC_ flink.c ffunc.o 1>/dev/null 2>/dev/null; then
    AC_MSG_RESULT(uppercase with underscore)
    AC_DEFINE_UNQUOTED(F77_DGEMM_SYMBOL,  DGEMM_)
  fi
  /bin/rm -f ffunc.f ffunc.o flink.c flink.o ffunc
else
  AC_MSG_RESULT(guessing lowercase with underscore)
fi

dnl ----------- NIAMA library checks --------------

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_CHECK_PROG(NIAMACFG,niama-config,niama-config)
if test X$NIAMACFG != X; then
  LIBSSAV="$LIBS"
  CPPSAV="$CPPFLAGS"
  NIAMALIBS="`niama-config --libs` `niama-config --corbalibs`"
  CPPFLAGS="$CPPFLAGS `niama-config --cppflags`"
  LIBS="$NIAMALIBS $LIBS"
AC_TRY_LINK([
#include <niama.h>
],[
  exit(0);
],[
  AC_DEFINE(HAVE_NIAMA)
],[
  LIBS="$LIBSSAV"
  CPPFLAGS="$CPPSAV"
]
)
fi
AC_LANG_RESTORE

dnl --------- Checks that must be done last. ---------

AC_MSG_CHECKING([for special flags that must be set last])
case $target in
  i860-intel-puma*)
    LDFLAGS="$LDFLAGS -mpuma -mnoieee"
    AC_MSG_RESULT(paragon puma)
  ;;
  i686-intel-cougar*)
    LDFLAGS="$LDFLAGS -mcougar -mnoieee"
    AC_MSG_RESULT(teraflop cougar)
  ;;
  i860-intel-sunmos*)
    LDFLAGS="$LDFLAGS -msunmos -mnoieee"
    AC_MSG_RESULT(paragon sunmos)
  ;;
  i860-intel-osf*)
    LDFLAGS="$LDFLAGS -mnx -mnoieee"
    AC_MSG_RESULT(osf paragon)
  ;;
changequote(<<, >>)dnl
  mips4-sgi-irix6.[01]* | r8000-sgi-irix6.[01]* | r10000-sgi-irix6.[01]*)
changequote([, ])dnl
    if test X$CXX = XCC; then
      CXX="\$(SRCTOPDIR)/bin/CCfix CC"
    fi
    AC_MSG_RESULT("old mips4-sgi-irix r8000-sgi-irix or r10000-sgi-irix")
  ;;
  rs6000-*)
    if test "X$CXX" = XmpCC -o X$CXX = XmpCC_r; then
      CXX="\$(SRCTOPDIR)/bin/xlCfix mpCC"
    fi
    if test "X$CXX" = XxlC; then
      CXX="\$(SRCTOPDIR)/bin/xlCfix xlC"
    fi
    AC_MSG_RESULT(rs6000)
  ;;
  *)
    AC_MSG_RESULT(none)
  ;;
esac

dnl --------- Find the list of all sc libraries. ---------

SCLIBS=`$PERL $srcdir/bin/listlibs.pl -I$srcdir/src/lib -DLIBSUF=$LIBSUF $srcdir/src/lib`
AC_SUBST(SCLIBS)

dnl --------- Defined the library directory macros. ---------

AC_DEFINE_UNQUOTED(SRC_SCLIBDIR, "$srcdir/lib")
AC_DEFINE_UNQUOTED(INSTALLED_SCLIBDIR, "$prefix/lib")

dnl --------- Create the stub Makefiles. ---------

$PERL $srcdir/bin/objectdir.pl $srcdir

AC_OUTPUT(lib/LocalMakefile bin/sc-config)
chmod +x bin/sc-config
